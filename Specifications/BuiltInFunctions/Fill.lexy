enum SimpleEnum
  First
  Next

function SimpleTarget
  Parameters
    number NumberValue
    string StringValue
    boolean BooleanValue
    date DateValue
    SimpleEnum EnumValue
  Results
    number NumberResult
    string StringResult
    boolean BooleanResult
    date DateResult
    SimpleEnum EnumResult
  Code
    NumberResult = NumberValue
    StringResult = StringValue
    BooleanResult = BooleanValue
    EnumResult = EnumValue

scenario FillNoParameters
  function
    Parameters
    Results
    Code
      var parameters = fill(SimpleTarget.Parameters)
  ExpectErrors
    "Invalid parameter mapping. No parameter could be mapped from variables."

scenario FillSingleParameters
  function
    Parameters
      number NumberValue
    Results
      number NumberResult
    Code
      var parameters = fill(SimpleTarget.Parameters)
      NumberResult = parameters.NumberValue
  Parameters
    NumberValue = 5
  Results
    NumberResult = 5

scenario FillMultipleParameters
// only corresponding parameters should be mapped
  function
    Parameters
      number NumberValue
      string StringValue
      boolean BooleanValueOther          // not mapped
      date DateValueOther                // not mapped
      SimpleEnum EnumValueOther          // not mapped
    Results
      number NumberResult
      string StringResult
      boolean BooleanResult
      date DateResult
      SimpleEnum EnumResult
    Code
      var parameters = fill(SimpleTarget.Parameters)
      NumberResult = parameters.NumberValue
      StringResult = parameters.StringValue
      BooleanResult = parameters.BooleanValue
      DateResult = parameters.DateValue
      EnumResult = parameters.EnumValue
  Parameters
    NumberValue = 5
    StringValue = "abc"
    BooleanValueOther = true
    DateValueOther = d"2024-12-27T14:39:45"
    EnumValueOther = SimpleEnum.Next
  Results
    NumberResult = 5
    StringResult = "abc"
    BooleanResult = false                    // not mapped
    DateResult = d"0001-01-01T00:00:00"      // not mapped
    EnumResult = SimpleEnum.First            // not mapped


scenario FillInIfScopeParameters
  function
    Parameters
      boolean Evil
      number NumberValueParent
    Results
      number NumberResult
    Code
      if Evil
        number NumberValue = NumberValueParent
        var parameters = fill(SimpleTarget.Parameters)
        NumberResult = parameters.NumberValue
  Parameters
    NumberValueParent = 5
    Evil = true
  Results
    NumberResult = 5
