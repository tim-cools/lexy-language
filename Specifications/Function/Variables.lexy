enum SimpleEnum
  First
  Next

function RootDeclarationFunction
  results
    number NumberResult
    string StringResult
    boolean BooleanResult
    date DateResult
    SimpleEnum EnumResult
  number numberValue = 123
  string stringValue = "abc"
  boolean booleanValue = true
  date dateValue = d"2024-12-24T13:49:00"
  SimpleEnum enumValue = SimpleEnum.Next

  NumberResult = numberValue
  StringResult = stringValue
  BooleanResult = booleanValue
  DateResult = dateValue
  EnumResult = enumValue

scenario RootDeclaration
  function RootDeclarationFunction
  results
    NumberResult = 123
    StringResult = "abc"
    BooleanResult = true
    DateResult = d"2024-12-24T13:49:00"
    EnumResult = SimpleEnum.Next


scenario ResultVariablesDefaultValues
// each declared result should have default value
  function
    results
      number NumberResult
      string StringResult
      boolean BooleanResult
      date DateResult
      SimpleEnum EnumResult
  results
    NumberResult = 0
    StringResult = ""
    BooleanResult = false
    DateResult = d"0001-01-01T00:00:00"
    EnumResult = SimpleEnum.First



scenario InCodeVariablesDefaultValues
// each declared variable should have default value
  function
    results
      number NumberResult
      string StringResult
      boolean BooleanResult
      date DateResult
      SimpleEnum EnumResult
    number numberValue
    string stringValue
    boolean booleanValue
    date dateValue
    SimpleEnum enumValue

    NumberResult = numberValue
    StringResult = stringValue
    BooleanResult = booleanValue
    DateResult = dateValue
    EnumResult = enumValue

  results
    NumberResult = 0
    StringResult = ""
    BooleanResult = false
    DateResult = d"0001-01-01T00:00:00"
    EnumResult = SimpleEnum.First


scenario ResultAssignmentWithInitializerAndDefaultValues
// each declared parameter variable should an initialized value
  function
    parameters
      number NumberParam = 1
      string StringParam = "1"
      boolean BooleanParam = true
      date DateParam = d"2024-12-25T00:00:00"
      SimpleEnum EnumParam = SimpleEnum.Next
    results
      number NumberResult
      string StringResult
      boolean BooleanResult
      date DateResult
      SimpleEnum EnumResult
    NumberResult = NumberParam
    StringResult = StringParam
    BooleanResult = BooleanParam
    DateResult = DateParam
    EnumResult = EnumParam

  results
    NumberResult = 1
    StringResult = "1"
    BooleanResult = true
    DateResult = d"2024-12-25T00:00:00"
    EnumResult = SimpleEnum.Next



function ScopedVariablesFunction
  parameters
    boolean Evil
  results
    number NumberResult
    string StringResult
    boolean BooleanResult
    date DateResult
    SimpleEnum EnumResult
  if Evil
    number numberValue = 123
    string stringValue = "abc"
    boolean booleanValue = true
    date dateValue = d"2024-12-24T13:49:00"
    SimpleEnum enumValue = SimpleEnum.Next

    NumberResult = numberValue
    StringResult = stringValue
    BooleanResult = booleanValue
    DateResult = dateValue
    EnumResult = enumValue

scenario ScopedVariables
  function ScopedVariablesFunction
  parameters
    Evil = true
  results
    NumberResult = 123
    StringResult = "abc"
    BooleanResult = true
    DateResult = d"2024-12-24T13:49:00"
    EnumResult = SimpleEnum.Next


function ScopedVariablesOutOfScopeFunction
// it should not be possible access the value of a variable out of it's scope
  parameters
    boolean Evil
  results
    number NumberResult

  if Evil
    number numberValue = 123

  NumberResult = numberValue

scenario ScopedVariablesOutOfScope
  function ScopedVariablesOutOfScopeFunction
  expectErrors
    "Invalid identifier: 'numberValue'"


function ScopedVariablesManyScopesFunction
// it should be possible to declare variables with same name in different sub scopes
  parameters
    boolean Evil = false
  results
    number NumberResult

  switch Evil
    case true
      if Evil
        number numberValue = 123
      else
        number numberValue = 456
    default
      number numberValue = 789

  NumberResult = 777

scenario ScopedVariablesManyScopes
  function ScopedVariablesManyScopesFunction
  results
    NumberResult = 777



scenario ResultsShouldNotHaveADefaultValue
  function
    results
      date Value = d"2024-12-25T16:57:45"
  expectErrors
    "Result variable 'Value' should not have a default value."



scenario UnknownVariable
  function
    parameters
      date Value
    Result = Date.Day(Value)
  expectErrors
    "Invalid identifier: 'Result'"
    "Unknown variable name: 'Result'."

scenario UnknownVariableInIfScope
  function
    parameters
      boolean Evil
      date Value
    if Evil
      number Result
    Result = Date.Day(Value)
  expectErrors
    "Invalid identifier: 'Result'"
    "Unknown variable name: 'Result'."

scenario InvalidAssignmentType
  function
    parameters
      date Value
    results
      date Result
    Result = Date.Day(Value)
  expectErrors
    "Variable 'Result' of type 'date' is not assignable from expression of type 'number'."



scenario VariableWithIfType
  function
    parameters
      date Value
    If Evil
      number Result
    Result = Date.Day(Value)
  expectErrors
    "Invalid type: 'If'"
    "Invalid variable type 'If'"