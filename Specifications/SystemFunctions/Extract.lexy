enum SimpleEnum
  First
  Next

function SimpleTarget
  parameters
    number NumberValue
    string StringValue
    boolean BooleanValue
    date DateValue
    SimpleEnum EnumValue
  results
    number NumberResult
    string StringResult
    boolean BooleanResult
    date DateResult
    SimpleEnum EnumResult
  NumberResult = NumberValue
  StringResult = StringValue
  BooleanResult = BooleanValue
  EnumResult = EnumValue

scenario ExtractWithTypeReference
  function
    parameters
    results
    extract(SimpleTarget.Parameters)
  expectErrors
    "Invalid variable argument. extract expects 1 argument. extract(variable)"

scenario ExtractNoParameters
  function
    parameters
    results
    SimpleTarget.Parameters params
    extract(params)
  expectErrors
    "Invalid parameter mapping. No parameter could be mapped from variables."

scenario ExtractSingleParameters
  function
    parameters
      number NumberValue
    results
      number NumberResult
    SimpleTarget.Results params
    params.NumberResult = NumberValue
    extract(params)
  parameters
    NumberValue = 5
  results
    NumberResult = 5

scenario ExtractMultipleParameters
// only corresponding parameters should be mapped
  function
    parameters
      number NumberValue
      string StringValue
      boolean BooleanValue
      date DateValue
      SimpleEnum EnumValue
    results
      number NumberResult
      string StringResult
      boolean BooleanResultOther    // not mapped
      date DateResultOther          // not mapped
      SimpleEnum EnumResultOther    // not mapped
    SimpleTarget.Results params
    params.NumberResult = NumberValue
    params.StringResult = StringValue
    params.BooleanResult = BooleanValue
    params.DateResult = DateValue
    params.EnumResult = EnumValue
    extract(params)
  parameters
    NumberValue = 5
    StringValue = "abc"
    BooleanValue = true
    DateValue = d"2024-12-27T14:39:45"
    EnumValue = SimpleEnum.Next
  results
    NumberResult = 5
    StringResult = "abc"
    BooleanResultOther = false                // not mapped
    DateResultOther = d"0001-01-01T00:00:00"  // not mapped
    EnumResultOther = SimpleEnum.First        // not mapped




scenario ExtractInIfScopeParameters
  function
    parameters
      boolean Evil
      number NumberValue
    results
      number NumberResultParent
    if Evil
      number NumberResult
      SimpleTarget.Results params
      params.NumberResult = NumberValue
      extract(params)
      NumberResultParent = NumberResult
  parameters
    NumberValue = 5
    Evil = true
  results
    NumberResultParent = 5



scenario ExtractMultipleParametersInvalidType
  function
    parameters
      boolean BooleanValue
    results
      number NumberResult
    SimpleTarget.Results params
    params.NumberResult = BooleanValue
  expectErrors
    "Variable 'params.NumberResult' of type 'number' is not assignable from expression of type 'boolean'."