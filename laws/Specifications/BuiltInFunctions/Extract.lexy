Enum: SimpleEnum
  First
  Next

Function: SimpleTarget
  Parameters
    number NumberValue
    string StringValue
    boolean BooleanValue
    date DateValue
    SimpleEnum EnumValue
  Results
    number NumberResult
    string StringResult
    boolean BooleanResult
    date DateResult
    SimpleEnum EnumResult
  Code
    NumberResult = NumberValue
    StringResult = StringValue
    BooleanResult = BooleanValue
    EnumResult = EnumValue

Scenario: ExtractWithTypeReference
  Function:
    Parameters
    Results
    Code
      extract(SimpleTarget.Parameters)
  ExpectError "Invalid variable argument. extract expects 1 argument. extract(variable)"

Scenario: ExtractNoParameters
  Function:
    Parameters
    Results
    Code
      SimpleTarget.Parameters parameters
      extract(parameters)
  ExpectError "Invalid parameter mapping. No parameter could be mapped from variables."

Scenario: ExtractSingleParameters
  Function:
    Parameters
      number NumberValue
    Results
      number NumberResult
    Code
      SimpleTarget.Results parameters
      parameters.NumberResult = NumberValue
      extract(parameters)
  Parameters
    NumberValue = 5
  Results
    NumberResult = 5

Scenario: ExtractMultipleParameters
# only corresponding parameters should be mapped
  Function:
    Parameters
      number NumberValue
      string StringValue
      boolean BooleanValue
      date DateValue
      SimpleEnum EnumValue
    Results
      number NumberResult
      string StringResult
      boolean BooleanResultOther    #not mapped
      date DateResultOther          #not mapped
      SimpleEnum EnumResultOther    #not mapped
    Code
      SimpleTarget.Results parameters
      parameters.NumberResult = NumberValue
      parameters.StringResult = StringValue
      parameters.BooleanResult = BooleanValue
      parameters.DateResult = DateValue
      parameters.EnumResult = EnumValue
      extract(parameters)
  Parameters
    NumberValue = 5
    StringValue = "abc"
    BooleanValue = true
    DateValue = d"2024-12-27T14:39:45"
    EnumValue = SimpleEnum.Next
  Results
    NumberResult = 5
    StringResult = "abc"
    BooleanResultOther = false                #not mapped
    DateResultOther = d"0001-01-01T00:00:00"  #not mapped
    EnumResultOther = SimpleEnum.First        #not mapped




Scenario: ExtractInIfScopeParameters
  Function:
    Parameters
      boolean Evil
      number NumberValue
    Results
      number NumberResultParent
    Code
      if Evil
        number NumberResult
        SimpleTarget.Results parameters
        parameters.NumberResult = NumberValue
        extract(parameters)
        NumberResultParent = NumberResult
  Parameters
    NumberValue = 5
    Evil = true
  Results
    NumberResultParent = 5



Scenario: ExtractMultipleParametersInvalidType
  Function:
    Parameters
      boolean BooleanValue
    Results
      number NumberResult
    Code
      SimpleTarget.Results parameters
      parameters.NumberResult = BooleanValue
  ExpectError "Variable 'parameters.NumberResult' of type 'number' is not assignable from expression of type 'boolean'."