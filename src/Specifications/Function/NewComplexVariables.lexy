Table: StringTable
  | string SearchValue | number ResultNumber | string ResultString | boolean ResultBoolean | date ResultDate          |
  | "a"                | 222                 | "222"               | false                 | d"2024-12-24T23:22:22"   |
  | "b"                | 333                 | "333"               | true                  | d"2024-12-24T23:33:33"   |
  | "c"                | 444                 | "444"               | false                 | d"2024-12-24T23:44:44"   |
  | "d"                | 555                 | "555"               | true                  | d"2024-12-24T23:55:55"   |

Scenario: NewTableRow
  Function:
    Results
      string StringResult
    Code
      StringTable.Row row
      StringResult = row.SearchValue
  Results
    StringResult = ""

Function: SimpleTarget
  Parameters
    string StringValue = "333"
  Results
    number NumberResult
    string StringResult
  Code
    NumberResult = 666
    StringResult = StringValue

Scenario: NewFunctionParameters
  Function:
    Results
      string StringResult
    Code
      SimpleTarget.Parameters row
      StringResult = row.StringValue
  Results
    StringResult = "333"

Type: CustomType
  number NumberValue = 123
  string StringValue = "abc"

Scenario: NewCustomType
  Function:
    Results
      string StringResult
      number NumberResult
    Code
      CustomType type
      NumberResult = type.NumberValue
      StringResult = type.StringValue
  Results
    StringResult = "abc"
    NumberResult = 123

Scenario: NewCustomTypeAssign
  Function:
    Results
      string StringResult
      number NumberResult
    Code
      CustomType type
      type.NumberValue = 456
      type.StringValue = "def"
      NumberResult = type.NumberValue
      StringResult = type.StringValue
  Results
    StringResult = "def"
    NumberResult = 456

Scenario: NewFunctionParametersInvalidMember
  Function:
    Results
      string StringResult
    Code
      SimpleTarget.Parameters row
      StringResult = row.ResultNumber
  ExpectError "Unknown variable name: 'row.ResultNumber'"

Scenario: NewFunctionResults
  Function:
    Results
      number NumberResult
    Code
      var row = new(SimpleTarget.Results)
      NumberResult = row.NumberResult
  Results
    NumberResult = 0

Scenario: NewFunctionResultsInvalidMember
  Function:
    Results
      number NumberResult
    Code
      SimpleTarget.Results row
      NumberResult = row.ResultNumber
  ExpectError "Unknown variable name: 'row.ResultNumber'"

