Enum: SimpleEnum
  First
  Next

Function: RootDeclarationFunction
  Results
    number NumberResult
    string StringResult
    boolean BooleanResult
    date DateResult
    SimpleEnum EnumResult
  Code
    number numberValue = 123
    string stringValue = "abc"
    boolean booleanValue = true
    date dateValue = d"2024-12-24T13:49:00"
    SimpleEnum enumValue = SimpleEnum.Next

    NumberResult = numberValue
    StringResult = stringValue
    BooleanResult = booleanValue
    DateResult = dateValue
    EnumResult = enumValue

Scenario: RootDeclaration
  Function RootDeclarationFunction
  Results
    NumberResult = 123
    StringResult = "abc"
    BooleanResult = true
    DateResult = d"2024-12-24T13:49:00"
    EnumResult = SimpleEnum.Next


Scenario: ResultVariablesDefaultValues
# each declared result should have default value
  Function:
    Results
      number NumberResult
      string StringResult
      boolean BooleanResult
      date DateResult
      SimpleEnum EnumResult
  Results
    NumberResult = 0
    StringResult = ""
    BooleanResult = false
    DateResult = d"0001-01-01T00:00:00"
    EnumResult = SimpleEnum.First



Scenario: InCodeVariablesDefaultValues
# each declared variable should have default value
  Function:
    Results
      number NumberResult
      string StringResult
      boolean BooleanResult
      date DateResult
      SimpleEnum EnumResult
    Code
      number numberValue
      string stringValue
      boolean booleanValue
      date dateValue
      SimpleEnum enumValue

      NumberResult = numberValue
      StringResult = stringValue
      BooleanResult = booleanValue
      DateResult = dateValue
      EnumResult = enumValue

  Results
    NumberResult = 0
    StringResult = ""
    BooleanResult = false
    DateResult = d"0001-01-01T00:00:00"
    EnumResult = SimpleEnum.First


Scenario: ResultAssignmentWithInitializerAndDefaultValues
# each declared parameter variable should an initialized value
  Function:
    Parameters
      number NumberParam = 1
      string StringParam = "1"
      boolean BooleanParam = true
      date DateParam = d"2024-12-25T00:00:00"
      SimpleEnum EnumParam = SimpleEnum.Next
    Results
      number NumberResult
      string StringResult
      boolean BooleanResult
      date DateResult
      SimpleEnum EnumResult
    Code
      NumberResult = NumberParam
      StringResult = StringParam
      BooleanResult = BooleanParam
      DateResult = DateParam
      EnumResult = EnumParam

  Results
    NumberResult = 1
    StringResult = "1"
    BooleanResult = true
    DateResult = d"2024-12-25T00:00:00"
    EnumResult = SimpleEnum.Next



Function: ScopedVariablesFunction
  Parameters
    boolean Evil
  Results
    number NumberResult
    string StringResult
    boolean BooleanResult
    date DateResult
    SimpleEnum EnumResult
  Code
    if Evil
      number numberValue = 123
      string stringValue = "abc"
      boolean booleanValue = true
      date dateValue = d"2024-12-24T13:49:00"
      SimpleEnum enumValue = SimpleEnum.Next

      NumberResult = numberValue
      StringResult = stringValue
      BooleanResult = booleanValue
      DateResult = dateValue
      EnumResult = enumValue

Scenario: ScopedVariables
  Function ScopedVariablesFunction
  Parameters
    Evil = true
  Results
    NumberResult = 123
    StringResult = "abc"
    BooleanResult = true
    DateResult = d"2024-12-24T13:49:00"
    EnumResult = SimpleEnum.Next


Function: ScopedVariablesOutOfScopeFunction
# it should not be possible access the value of a variable out of it's scope
  Parameters
    boolean Evil
  Results
    number NumberResult
  Code
    if Evil
      number numberValue = 123

    NumberResult = numberValue

Scenario: ScopedVariablesOutOfScope
  Function ScopedVariablesOutOfScopeFunction
  ExpectError "Unknown variable name: 'numberValue'"


Function: ScopedVariablesManyScopesFunction
# it should be possible to declare variables with same name in different sub scopes
  Parameters
    boolean Evil = false
  Results
    number NumberResult
  Code
    switch Evil
      case true
        if Evil
          number numberValue = 123
        else
          number numberValue = 456
      default
        number numberValue = 789

    NumberResult = 777

Scenario: ScopedVariablesManyScopes
  Function ScopedVariablesManyScopesFunction
  Results
    NumberResult = 777



Scenario: ResultsShouldNotHaveADefaultValue
  Function:
    Results
      date Value = d"2024-12-25T16:57:45"
    Code
      Result = DAY(Value)
  ExpectError "Result variable 'Value' should not have a default value."



Scenario: UnknownVariable
  Function:
    Parameters
      date Value
    Code
      Result = DAY(Value)
  ExpectError "Unknown variable name: 'Result'"

Scenario: UnknownVariableInIfScope
  Function:
    Parameters
      date Value
    Code
      if Evil
        number Result
      Result = DAY(Value)
  ExpectError "Unknown variable name: 'Result'"


Scenario: InvalidAssignmentType
  Function:
    Parameters
      date Value
    Results
      date Result
    Code
      Result = DAY(Value)
  ExpectError "Variable 'Result' of type 'date' is not assignable from expression of type 'number'."



Scenario: VariableWithIfType
  Function:
    Parameters
      date Value
    Code
      If Evil
        number Result
      Result = DAY(Value)
  ExpectError "Invalid variable type 'If'"